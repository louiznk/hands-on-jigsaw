subprojects {
    afterEvaluate {
        repositories {
            jcenter()
        }
        sourceCompatibility = 9
        targetCompatibility = 9

        compileJava {

            inputs.property("moduleName", moduleName) // add modulename properties
            doFirst {
                // use module-path instead of classpath (and reset it)
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files() // clean
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'junit',
                        '--add-reads', "$moduleName=junit",
//                        '--add-reads', "$moduleName=hamcrest.core",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=junit",
                        '--add-reads', "$moduleName=hamcrest.core",
                        '--add-exports', "$moduleName/$moduleName=junit",
                        '--add-exports', "$moduleName/$moduleName=hamcrest.core",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }

            // listen to events in the test execution lifecycle
            beforeTest { descriptor ->
                logger.lifecycle("Running test: " + descriptor)
            }

            // listen to standard out and standard error of the test JVM(s)
            onOutput { descriptor, event ->
                logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
            }
        }

    }
}
